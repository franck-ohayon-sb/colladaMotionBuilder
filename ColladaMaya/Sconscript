#Sconscript for ColladaMaya Library for Linux
relPath = "../../.."
execfile(relPath + "/../SconsCommon.py")

#Create the Environment which creates the compile and linker command lines.
ifdebug = ARGUMENTS.get('debug', 0)
ifversion = ARGUMENTS.get('version', 70) #Default is Maya7.0
ifshared = ARGUMENTS.get('shared', 0) #Default is static, both should be built however.
if int(ifversion) == 70:
	env = Environment(CXX = '/opt/gcc334/bin/g++334')
else:
	env = Environment(CXX = '/opt/gcc402/bin/g++402')

#Add the compiler and linker flags and include search path
env.Append(CPPPATH = [relPath, relPath + "/../FCollada", relPath + "/../FCollada/LibXML/include", relPath + "/../External"])
if int(ifversion) == 70:
    env.Append(CPPPATH = ['/usr/aw/maya7.0/include'])
elif int(ifversion) == 80:
    env.Append(CPPPATH = ['/usr/aw/maya8.0/include'])
else:
    env.Append(CPPPATH = ['/usr/autodesk/maya8.5/include'])

#Add the macros defined for all the builds
env.Append(CPPDEFINES = ['linux', '_BOOL', 'LINUX', 'REQUIRE_IOSTREAM', 'GL_GLEXT_PROTOTYPES'])

#Add the macros and flags defined only for DEBUG, RETAIL or UNICODE
if int(ifdebug):
    env.Append(CPPDEFINES = ['_DEBUG'])
    env.Append(CCFLAGS = ['-O0', '-g'])
else:
    env.Append(CPPDEFINES = ['NDEBUG', 'RETAIL'])
    env.Append(CCFLAGS = ['-O2'])
if int(ifversion) == 85:
    env.Append(CPPDEFINES = ['UNICODE'])

#List of the source code to compile, and make a library out of it
list = []
FileListAppend(list, relPath, "", "cpp")
FileListAppend(list, relPath, "ColladaFX", "cpp")
FileListAppend(list, relPath, "TranslatorHelpers", "cpp")

libs = ["GL", "GLU", "Cg", "CgGL", "Foundation", "OpenMaya", "OpenMayaUI", "OpenMayaAnim", "OpenMayaFX", "OpenMayaRender"]
if int(ifdebug):
    if int(ifversion) == 85:
        libs.append("FColladaSUD")
    else:
        libs.append("FColladaSD")
else:
    if int(ifversion) == 85:
        libs.append("FColladaSUR")
    else:
        libs.append("FColladaSR")

libsPath = [relPath + "/../External/Cg/Bin", relPath + "/../FCollada/Output"]
if int(ifversion) == 70: libsPath.append("/usr/aw/maya7.0/lib")
elif int(ifversion) == 80: libsPath.append("/usr/aw/maya8.0/lib")
else: libsPath.append("/usr/autodesk/maya8.5/lib")

name1 = 'COLLADA'
name2 = 'COLLADAexport'

if int(ifshared):
	ColladaMaya1 = env.SharedLibrary(name1, list, LIBPATH=libsPath, LIBS=libs)
	env.Install('../', ColladaMaya1)
else:
	ColladaMaya2 = env.StaticLibrary(name2, list)
	env.Install('../', ColladaMaya2) 
